/* *****************************************************************************
 * Ruin Rumble - Made by Kayo
 * *****************************************************************************
 * A closed map with a center ruin to fight over.
 * Strike your Enemies and plunder the ruins to take what remains!
 * ***************************************************************************** 
 * git: https://github.com/Gatsuca/AOE2_RMS
 * ***************************************************************************** */

/* Biome Selection from F_seasons.inc */
if ANTIQUITY_MODE
	start_random
		percent_chance 10 	#define PH_ALPINE
		percent_chance 10 	#define PH_SPRING
		percent_chance 10 	#define PH_SPRING_C
		percent_chance 24 	#define PH_MEDISOUTH
		percent_chance 12 	#define PH_DESERT
		percent_chance 8 	#define PH_AFRICAN		
		percent_chance 8 	#define PH_AFRICAN_E
		percent_chance 12 	#define PH_AUTUMN
		percent_chance 6 	#define PH_FROZEN
	end_random
else 
	start_random
		percent_chance 8 	#define PH_ALPINE
		percent_chance 5 	#define PH_SPRING
		percent_chance 5 	#define PH_SPRING_C
		percent_chance 14 	#define PH_MEDISOUTH
		percent_chance 4 	#define PH_TROPHICALSOUTH
		percent_chance 4 	#define PH_TROPHICALSOUTH_B
		percent_chance 8 	#define PH_TROPHICALEAST
		percent_chance 10 	#define PH_DESERT
		percent_chance 8 	#define PH_AFRICAN		
		percent_chance 8 	#define PH_AFRICAN_E
		percent_chance 6 	#define PH_ASIAN
		percent_chance 6 	#define PH_ASIAN_B
		percent_chance 5 	#define PH_AUTUMN
		percent_chance 5 	#define PH_AUTUMN_B
		percent_chance 4 	#define PH_FROZEN
	end_random
endif
#include_drs F_seasons.inc

/* Declaration for storing map size scaling value by LENGTH */
if MAPSIZE_LUDICROUS 		#const LENGTH 480	/* LUDICROUS 480x480 mapsize */
elseif MAPSIZE_MONSTROUS	#const LENGTH 400	/* MONSTROUS 400x400 mapsize */
elseif MAPSIZE_INCREDIBLE 	#const LENGTH 360	/* INCREDIBLE 360x360 mapsize */
elseif MAPSIZE_COLOSSAL 	#const LENGTH 320	/* COLOSSAL 320x320 mapsize */
elseif MAPSIZE_ENORMOUS 	#const LENGTH 300	/* ENORMOUS 300x300 mapsize */
elseif MAPSIZE_MASSIVE 		#const LENGTH 276	/* MASSIVE 276x276 mapsize */
elseif MAPSIZE_GIANT 		#const LENGTH 252	/* GIANT 252x252 mapsize */
elseif MAPSIZE_HUGE 		#const LENGTH 240	/* HUGE 240x240 mapsize */
elseif MAPSIZE_LARGE 		#const LENGTH 220	/* LARGE 220x220 mapsize */
elseif MAPSIZE_NORMAL 		#const LENGTH 200	/* NORMAL 200x200 mapsize */
elseif MAPSIZE_MEDIUM 		#const LENGTH 168	/* MEDIUM 168x168 mapsize */
elseif MAPSIZE_SMALL 		#const LENGTH 144	/* SMALL 144x144 mapsize */
elseif MAPSIZE_TINY 		#const LENGTH 120	/* TINY 120x120 mapsize */
elseif MAPSIZE_MINI 		#const LENGTH 80	/* MINI 80x80 mapsize */
endif
/* Terrain and Object constant declarations */
#const TEMPOBJECT           278		/* Temporary object placeholder */
#const TEMP_TERRAIN         128		/* Temporary terrain placeholder */
#const T_WORKSHOP           1647	/* Trading Workshop building */
#const GOODS                1321	/* Trade goods object */
#const MARKET_STALL         2284	/* Market stall building */
#const HOUSE_RUBBLE3        1435	/* Ruined house variant 3 */
#const CASTLE_RUINS         1837    /* Castle ruins object */

/* PLAYER_SETUP: Configures how players are positioned and global parameters */
<PLAYER_SETUP>
if FIXED_POSITIONS
grouped_by_team  					/* Players positioned together in circle with variation */
else
random_placement  					/* Players positioned randomly in circle with variation */
endif
ai_info_map_type ARENA 0 1 0  		/* Tells AI: MapType=CUSTOM, IsNomad=0, IsMichi=0, ShowType=0 */
behavior_version 1					/* Used for versioning changes that might affect how existing maps generate */
/* City civilization selector - based upon biome style */
if PH_ALPINE				set_gaia_civilization 3
elseif PH_SPRING			set_gaia_civilization 1
elseif PH_SPRING_C			set_gaia_civilization 3
elseif PH_MEDISOUTH			set_gaia_civilization 7
elseif PH_TROPHICALSOUTH	set_gaia_civilization 15
elseif PH_TROPHICALSOUTH_B	set_gaia_civilization 15
elseif PH_TROPHICALEAST		set_gaia_civilization 5
elseif PH_DESERT			set_gaia_civilization 8	
elseif PH_AFRICAN			set_gaia_civilization 25
elseif PH_AFRICAN_E			set_gaia_civilization 25
elseif PH_ASIAN 			set_gaia_civilization 6
elseif PH_ASIAN_B			set_gaia_civilization 20
elseif PH_AUTUMN			set_gaia_civilization 22
elseif PH_AUTUMN_B			set_gaia_civilization 3
elseif PH_FROZEN			set_gaia_civilization 3
else
endif

/* LAND_GENERATION: Creates large terrain areas including player starting areas */
<LAND_GENERATION>
base_terrain WOODIES					/* Sets default terrain covering entire map */

create_land
{
	terrain_type    DLC_ROADGRAVEL      /* Set the Terrain type for this land to road gravel */
	land_percent            100		    /* 100% of map area for the central city */
	land_position           50 50	    /* Land located in exact center of map */
	left_border             24          /* Left edge of land generation in percent */
    right_border            24          /* Right edge of land generation in percent */
    top_border              24          /* Top edge of land generation in percent */
    bottom_border           24          /* Bottom edge of land generation in percent */
    border_fuzziness        100		    /* 100% border adherence */
    clumping_factor         40		    /* The extent to which land growth prefers to clump together near existing tiles */
	land_id                 1		    /* Central city land id set to 1 */
}

create_player_lands
{
	terrain_type LAYER_A				/* Terrain type for player starting areas */
	/* DYNAMIC SCALING: Adjusts player land size based on map size */
	if LENGTH < 144
		base_size           15			/* 15-tile base size for tiny maps */
		land_percent        36			/* 36% land mass for player areas */
		circle_radius       38 0		/* No variance for small maps */
	elseif LENGTH 144
		base_size           16			/* 16-tile base size for small maps */
		land_percent        38			/* 38% land mass for player areas */
		circle_radius       43 1		/* 43% radius with 1 variance */
	elseif LENGTH 168
		base_size           18			/* 18-tile base size for medium maps */
		land_percent        40			/* 40% land mass for player areas */
		circle_radius       45 1		/* 45% radius with 1 variance */
	elseif LENGTH 200
		base_size           20			/* 20-tile base size for normal maps */
		land_percent        42			/* 42% land mass for player areas */
		circle_radius       46 1		/* 46% radius with 1 variance */
	elseif LENGTH 220
		base_size           21			/* 21-tile base size for large maps */
		land_percent        44			/* 44% land mass for player areas */
		circle_radius       47 1		/* 47% radius with 1 variance */
    elseif LENGTH 240
		base_size           22			/* 22-tile base size for huge maps */
		land_percent        45			/* 45% land mass for player areas */
		circle_radius       49 1		/* 48% radius with 1 variance */
	elseif LENGTH 252
		base_size           22			/* Default 22-tile base size */
		land_percent        46			/* 46% land mass for player areas */
		circle_radius       50 1		/* Default 50% radius with 1 variance */
    elseif LENGTH > 252
		base_size           22			/* Default 22-tile base size */
		land_percent        48			/* 48% land mass for player areas */
		circle_radius       51 1		/* Default 51% radius with 1 variance */    
	endif
	base_elevation          1			/* Slight elevation for player areas */
	clumping_factor         40			/* The extent to which land growth prefers to clump together near existing tiles */
	other_zone_avoidance_distance 24	/* 24-tile distance between player zones */
	if FIXED_POSITIONS
		set_zone_by_team			    /* Team members share same zone */
	else
	endif
	set_circular_base
}

/* ELEVATION_GENERATION: Create hills */
<ELEVATION_GENERATION>
create_elevation 4 								/* Sets max height allowed in create_elevation (0-16) */
{
	base_terrain 				WOODIES			/* Terrain type to apply elevation */
	set_scale_by_size							/* Scales tile count by map size */
	number_of_tiles 			1024			/* Amount of elevated tiles */
	number_of_clumps 			(LENGTH / 4)	/* Amount of hills - LENGTH divided by 4 */
	enable_balanced_elevation					/* To keep elevation balanced across entirety of map */
}
create_elevation 3 								/* Sets max height allowed in create_elevation (0-16) */
{
	base_terrain 				LAYER_A			/* Terrain type to apply elevation */
	set_scale_by_size							/* Scales tile count by map size */
	number_of_tiles 			512			    /* Amount of elevated tiles */
	number_of_clumps 			(LENGTH / 10)	/* Amount of hills - LENGTH divided by 10 */
	enable_balanced_elevation					/* To keep elevation balanced across entirety of map */
}
create_elevation 2 								/* Sets max height allowed in create_elevation (0-16) */
{
	base_terrain 				DLC_ROADGRAVEL	/* Terrain type to apply elevation */
	set_scale_by_size							/* Scales tile count by map size */
	number_of_tiles 			1024			/* Amount of elevated tiles */
	number_of_clumps 			(LENGTH / 10)	/* Amount of hills - LENGTH divided by 10 */
	enable_balanced_elevation					/* To keep elevation balanced across entirety of map */
}

/* TERRAIN_GENERATION: Replace terrains with other terrains */
<TERRAIN_GENERATION>
/* Color correction selection - Based upon Biome selected prior */
if PH_ALPINE				color_correction	CC_WINTER
elseif PH_SPRING			color_correction	CC_SPRING
elseif PH_SPRING_C			color_correction	CC_SPRING
elseif PH_MEDISOUTH			color_correction	CC_SUMMER
elseif PH_TROPHICALSOUTH	color_correction	CC_RAINFOREST
elseif PH_TROPHICALSOUTH_B	color_correction	CC_RAINFOREST
elseif PH_TROPHICALEAST		color_correction	CC_JUNGLE
elseif PH_DESERT			color_correction	CC_DESERT	
elseif PH_AFRICAN			color_correction	CC_SAVANNAH
elseif PH_AFRICAN_E			color_correction	CC_SAVANNAH
elseif PH_ASIAN 			color_correction	CC_SUMMER
elseif PH_ASIAN_B			color_correction	CC_STEPPES
elseif PH_AUTUMN			color_correction	CC_AUTUMN
elseif PH_AUTUMN_B			color_correction	CC_AUTUMN
elseif PH_FROZEN			color_correction	CC_ARCTIC
else
endif

create_terrain TEMP_TERRAIN
{
	base_terrain DLC_ROADGRAVEL	        /* Terrain type to create new terrain from */
	land_percent                100		/* 100% of map area covered in TEMP_TERRAIN */
	set_avoid_player_start_areas	    /* Avoids placing TEMP_TERRAIN in player starting areas */
}

create_terrain DLC_ROADGRAVEL
{
	base_terrain TEMP_TERRAIN			/* Terrain type to create new terrain from */
	land_percent                100		/* 100% coverage of DLC_ROADGRAVEL */
	number_of_clumps            9999	/* Maximum clumps for extensive road network */
	clumping_factor             30	    /* The extent to which terrain prefers to clump together near existing tiles */
	set_avoid_player_start_areas	    /* Avoids placing DLC_ROADGRAVEL in player starting areas */
	spacing_to_other_terrain_types 1	/* 1-tile distance to other terrain types */
}

create_terrain WOODIES_B
{
	base_terrain WOODIES			    /* Terrain type to create new terrain from */
	land_percent                16       /* Percentage of map covered in WOODIES */
	number_of_clumps            128	    /* Number of WOODIES_B clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
	clumping_factor             10		/* The extent to which terrain prefers to clump together near existing tiles */
}
create_terrain WOODIES
{
	base_terrain LAYER_A			    /* Terrain type to create new terrain from */
	number_of_tiles             256		/* Fixed number of WOODIES tiles */
	number_of_clumps            32		/* Number of WOODIES clumps */
	set_scale_by_groups			        /* Scales clumps by map size */
	set_avoid_player_start_areas	    /* Avoids placing WOODIES in player starting areas */
	spacing_to_other_terrain_types 1	/* 1-tile distance to other terrain types */
}

create_terrain WOODIES_B
{
	base_terrain LAYER_A			    /* Terrain type to create new terrain from */
	land_percent                1       /* Percentage of map covered in WOODIES */
	number_of_clumps            16	    /* Number of WOODIES_B clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
	clumping_factor             20		/* The extent to which terrain prefers to clump together near existing tiles */
}
create_terrain WOODIES
{
	base_terrain LAYER_A				/* Terrain type to create new terrain from */
	land_percent                2       /* Percentage of map covered in WOODIES */
	number_of_clumps            24		/* Number of WOODIES clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
	set_avoid_player_start_areas	    /* Avoids placing forests in player starting areas */
	spacing_to_other_terrain_types 1	/* 1-tile distance to other terrain types */
}

create_terrain WOODIES
{
	base_terrain DLC_ROADGRAVEL		    /* Terrain type to create new terrain from */
	land_percent                2		/* Percentage of map covered in WOODIES */
	number_of_clumps            2		/* Number of WOODIES clumps */
	clumping_factor             40		/* High clumping for dense WOODIES patches */
	set_scale_by_groups				    /* Scales clumps by map size */
	set_avoid_player_start_areas	    /* Avoids placing WOODIES in player starting areas */
	spacing_to_other_terrain_types 3	/* 3-tile distance to other terrain types */
}

create_terrain LAYER_B
{
	base_terrain LAYER_A			    /* Terrain type to create new terrain from */
	land_percent                20		/* Percentage of map covered in LAYER_B */
	number_of_clumps            256		/* Number of LAYER_B clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
	terrain_mask                2		/* 2 - new terrain is masked over the base terrain */
}

create_terrain LAYER_E	 
{
	base_terrain DLC_ROADGRAVEL		    /* Terrain type to create new terrain from */
	land_percent                4		/* Percentage of map covered in LAYER_E */
	number_of_clumps            64		/* Number of LAYER_E clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
}

create_terrain LAYER_C
{
	base_terrain DLC_ROADGRAVEL		    /* Terrain type to create new terrain from */
	land_percent                4		/* Percentage of map covered in LAYER_C */
	number_of_clumps            64		/* Number of LAYER_C clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
}

create_terrain LAYER_D
{
	base_terrain DLC_ROADGRAVEL		    /* Terrain type to create new terrain from */
	land_percent                4		/* Percentage of map covered in LAYER_D */
	number_of_clumps            64		/* Number of LAYER_D clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
}

create_terrain LAYER_A
{
	base_terrain DLC_ROADGRAVEL		    /* Terrain type to create new terrain from */
	land_percent                4		/* Percentage of map covered in LAYER_A */
	number_of_clumps            64		/* Number of clumps */
	set_scale_by_groups				    /* Scales clumps by map size */
}

/* CONNECTION_GENERATION: Draws connections between lands and players */
<CONNECTION_GENERATION>
accumulate_connections 			/* Allows you to replace terrains created in previous connections with new terrains */
if FIXED_POSITIONS
create_connect_teams_lands 
{
	/* Replace all terrain in path with corresponding dirt */
	if PH_FROZEN
	default_terrain_replacement DIRT_SNOW			
	else
	default_terrain_replacement DIRT2					
	endif						
	/* Terrain cost scale of 1-20 - lower values are more favorable - 0 = impassable  */ 
	terrain_cost 		LAYER_A 		1		
	terrain_cost 		LAYER_B 		1		
	terrain_cost 		LAYER_C 		1		
	terrain_cost 		WOODIES 		8		
	terrain_cost 		WOODIES_B 		8
    terrain_cost        TEMP_TERRAIN    0		
}
else
endif
create_connect_to_nonplayer_land 
{
	/* Replace all terrain in path with corresponding dirt */
	if PH_FROZEN
	default_terrain_replacement DIRT_SNOW			
	else
	default_terrain_replacement DIRT2				
	endif					
	/* Terrain cost scale of 1-20 - lower values are more favorable - 0 = impassable  */ 
	terrain_cost 		LAYER_A 		1		
	terrain_cost 		LAYER_B 		1		
	terrain_cost 		LAYER_C 		1		
	terrain_cost 		WOODIES 		8		
	terrain_cost 		WOODIES_B 		8
    terrain_cost        TEMP_TERRAIN    10	
}

/* OBJECTS_GENERATION: Places units, buildings, and resources on the map */
<OBJECTS_GENERATION>
/* City wall generation */
create_object WALL 
{
	number_of_objects       9999		/* Maximum number of wall segments */
	terrain_to_place_on TEMP_TERRAIN	/* Places walls on desert terrain */
	set_gaia_object_only			    /* Walls belong to Gaia (neutral) */
	set_gaia_unconvertible			    /* Walls cannot be converted */
	actor_area              99 			/* Actor area ID 5 */
	actor_area_radius       5			/* 5-tile radius for actor area */
}
/* Setting up player starting objects from GeneratingObjects.inc */
#define GNR_NORMALTC					/* Standard Town Center placement */
#define GNR_STARTVILLS					/* Starting Villagers */
#define GNR_CLASSICSCOUT				/* Classic Scout placement */

#define GNR_REGICIDECLASSIC				/* Regicide game mode support */

#define GNR_GIVEHERD_A					/* Herdable animals type A */
#define GNR_GIVEHERD_B					/* Herdable animals type B */

#define GNR_HERD4CLASSIC_A				/* Classic herd placement A */
#define GNR_HERD22CLASSIC_B				/* Classic herd placement B */

#define GNR_GIVEHUNT					/* Huntable animals */
#define GNR_HUNT5MODERN					/* Modern 5 huntable placement */

#define GNR_GIVERELICS					/* Relic placement */
#define GNR_RELICMODERN_MEDIUM_SA		/* Modern relic placement medium distance in a smaller area */
#define GNR_RELPLACE_LAYERC				/* Relic placement on LAYER_C terrain */

#define GNR_GIVELURES					/* Lure animals */
#define GNR_CLASSICLURES				/* Classic lure animal placement */

#define GNR_STARTINGGOLD				/* Starting gold resources */
#define GNR_STARTINGSTONE				/* Starting stone resources */

#define GNR_STARTGOLD43M				/* Starting gold for 4 objects 3 radius */
#define GNR_STARTSTONE32M				/* Starting stone for 3 objects 2 radius */

#define GNR_RESCLOSE					/* Close resource placement */

#define GNR_6KER						/* 6 Bushes start */
#define GNR_HOMESTRAGGLE				/* Home straggler trees */

#define GNR_NORMALBIRDS					/* Normal bird placement */

#include_drs GeneratingObjects.inc		/* Includes the actual object generation code */

create_object CASTLE_RUINS 
{
	number_of_objects           1		    /* Number of CASTLE_RUINS per group */
	number_of_groups            2		    /* Number of CASTLE_RUINS groups */
	set_scaling_to_map_size
	min_distance_group_placement 3	        /* Minimum 3 tiles between castle ruins */
	set_gaia_object_only			        /* Castle ruins belong to Gaia */
	terrain_to_place_on LAYER_E		        /* Places on LAYER_E terrain */
	actor_area                  9999	    /* Actor area ID 9999 */
	actor_area_radius           5		    /* 5-tile radius for actor area */
	temp_min_distance_group_placement 16	/* Avoids other castle ruins within 16 tiles */
}

create_object T_WORKSHOP 
{
	number_of_objects 1				        /* Only 1 T_WORKSHOP */
	set_gaia_object_only			        /* T_WORKSHOP belongs to Gaia */
	set_building_capturable			        /* T_WORKSHOP can be captured */
	place_on_specific_land_id 1		        /* Places on central city land (ID 1) */
    make_indestructible                     /* Makes object indestructible */
	find_closest					        /* Places at closest valid location */
	actor_area 700					        /* Actor area ID 700 */
	actor_area_radius 10			        /* 10-tile radius for actor area */
} 

create_object SIEGE_WORKSHOP
{
	number_of_objects 1			            /* 1 SIEGE_WORKSHOP per group */
	number_of_groups 5			            /* 5 groups total */
	set_scaling_to_map_size			        /* Scales with map size */
	temp_min_distance_group_placement 30	/* Avoids other SIEGE_WORKSHOP within 30 tiles */
	min_distance_to_players 25		        /* Minimum 25 tiles from players */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_actor_area 9999			        /* Avoids actor area 9999 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_forest_zone 3				        /* Avoids forests within 3 tiles */
	actor_area 5000				            /* Actor area ID 5000 */
	actor_area_radius 10			        /* 10-tile radius for actor area */
	min_distance_to_map_edge 10		        /* Minimum 10 tiles from map edge */
}

create_object BARRACKS_F 
{
	number_of_objects 1			            /* 1 BARRACKS_F per group */
	set_scaling_to_map_size			        /* Scales with map size */
	number_of_groups 5			            /* 5 groups total */
	temp_min_distance_group_placement 20	/* Avoids other BARRACKS_F within 20 tiles */
	min_distance_group_placement 3		    /* Minimum 3 tiles between BARRACKS_F in same group */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	actor_area_to_place_in 5000		        /* Must be placed within actor area 5000 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_forest_zone 3				        /* Avoids forests within 3 tiles */
}

create_object BLACKSMITH 
{
	number_of_objects 1			            /* 1 BLACKSMITH per group */
	set_scaling_to_map_size			        /* Scales with map size */
	number_of_groups 5			            /* 5 groups total */
	temp_min_distance_group_placement 20	/* Avoids other BLACKSMITH within 20 tiles */
	min_distance_group_placement 3		    /* Minimum 3 tiles between BLACKSMITH in same group */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	actor_area_to_place_in 5000		        /* Must be placed within actor area 5000 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_forest_zone 3				        /* Avoids forests within 3 tiles */
}

create_object STABLE 
{
	number_of_objects 1			            /* 1 STABLE per group */
	set_scaling_to_map_size			        /* Scales with map size */
	number_of_groups 5			            /* 5 groups total */
	temp_min_distance_group_placement 20	/* Avoids other STABLE within 20 tiles */
	min_distance_group_placement 3		    /* Minimum 3 tiles between STABLE in same group */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	actor_area_to_place_in 5000		        /* Must be placed within actor area 5000 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_forest_zone 3				        /* Avoids forests within 3 tiles */
}

create_object ARCHERY_RANGE 
{
	number_of_objects 1			            /* 1 ARCHERY_RANGE per group */
	set_scaling_to_map_size			        /* Scales with map size */
	number_of_groups 5			            /* 5 groups total */
	temp_min_distance_group_placement 20	/* Avoids other ARCHERY_RANGE within 20 tiles */
	min_distance_group_placement 3		    /* Minimum 3 tiles between ARCHERY_RANGE in same group */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	actor_area_to_place_in 5000		        /* Must be placed within actor area 5000 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_forest_zone 3				        /* Avoids forests within 3 tiles */
}

create_object MARKET_STALL 
{
	number_of_objects 64			        /* Up to 64 MARKET_STALL */
	set_scaling_to_map_size			        /* Scales with map size */
	temp_min_distance_group_placement 5	    /* Avoids other MARKET_STALL within 5 tiles */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	actor_area_to_place_in 5000		        /* Must be placed within actor area 5000 */
	actor_area 5002				            /* Actor area ID 5002 */
	actor_area_radius 1			            /* 1-tile radius for actor area */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
}

create_object GOODS 
{
	number_of_objects 64			        /* Up to 64 GOODS */
	set_scaling_to_map_size			        /* Scales with map size */
	temp_min_distance_group_placement 5	    /* Avoids other GOODS within 5 tiles */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	actor_area_to_place_in 5002		        /* Must be placed within actor area 5002 */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
}

create_object HOUSE_F 
{
	number_of_objects 1			            /* 1 HOUSE_F per group */
	number_of_groups 24			            /* 24 groups total */
	set_scaling_to_map_size			        /* Scales with map size */
	temp_min_distance_group_placement 4	    /* Avoids other HOUSE_F within 5 tiles */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_actor_area 9999			        /* Avoids actor area 9999 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_actor_area 5000			        /* Avoids actor area 5000 */
	actor_area 660				            /* Actor area ID 660 */
	actor_area_radius 3			            /* 3-tile radius for actor area */
} 

create_object HOUSE_RUBBLE3 
{
	number_of_objects 1			            /* 1 HOUSE_RUBBLE3 per group */
	number_of_groups 32			            /* 32 groups total */
	set_scaling_to_map_size			        /* Scales with map size */
	temp_min_distance_group_placement 4	    /* Avoids other HOUSE_RUBBLE3 within 5 tiles */
	set_gaia_object_only			        /* Belongs to Gaia */
	set_gaia_unconvertible			        /* Cannot be converted */
    make_indestructible
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
	avoid_actor_area 99				        /* Avoids actor area 5 */
	avoid_actor_area 9999			        /* Avoids actor area 9999 */
	avoid_actor_area 700			        /* Avoids actor area 700 */
	avoid_actor_area 5000			        /* Avoids actor area 5000 */
	actor_area 660				            /* Actor area ID 660 */
	actor_area_radius 3			            /* 3-tile radius for actor area */
	min_distance_to_map_edge 2		        /* Minimum 2 tiles from map edge */
}

create_object HUNTABLE 
{
	number_of_objects 2			            /* 2 HUNTABLE per group */
	number_of_groups 5			            /* 5 groups total */
	set_tight_grouping			            /* HUNTABLE spawn close together */
	set_scaling_to_map_size		            /* Scales with map size */
	temp_min_distance_group_placement 20	/* Avoids other HUNTABLE groups within 20 tiles */
	set_gaia_object_only		            /* Belongs to Gaia */
	actor_area_to_place_in 5002		        /* Must be placed within actor area 5002 */
	avoid_actor_area 99			            /* Avoids actor area 5 */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
} 

create_object LURABLE_A
{
	number_of_objects 2			            /* 2 LURABLE_A per group */
	number_of_groups 5			            /* 5 groups total */
	set_tight_grouping			            /* LURABLE_A spawn close together */
	set_scaling_to_map_size		            /* Scales with map size */
	temp_min_distance_group_placement 20	/* Avoids other LURABLE_A groups within 20 tiles */
	set_gaia_object_only		            /* Belongs to Gaia */
	actor_area_to_place_in 5002		        /* Must be placed within actor area 5002 */
	avoid_actor_area 99			            /* Avoids actor area 5 */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
} 
	
create_object LURABLE_B	
{		
    number_of_objects 2			            /* 2 LURABLE_B per group */		
    number_of_groups 5			            /* 5 groups total */		
    set_tight_grouping			            /* LURABLE_B spawn close together */
	set_scaling_to_map_size		            /* Scales with map size */
	temp_min_distance_group_placement 20	/* Avoids other LURABLE_B groups within 20 tiles */
	set_gaia_object_only		            /* Belongs to Gaia */
	actor_area_to_place_in 5002		        /* Must be placed within actor area 5002 */
	avoid_actor_area 99			            /* Avoids actor area 5 */
	terrain_to_place_on DLC_ROADGRAVEL	    /* Places on DLC_ROADGRAVEL terrain */
}

create_object PREDATOR_A
{
	number_of_objects 1			            /* 1 predator per group */
	set_scaling_to_map_size		            /* Scales with map size */
	set_gaia_object_only			        /* Predator belongs to Gaia */
	set_place_for_every_player		        /* Places predator for each player */
	min_distance_to_players 35		        /* Minimum 35 tiles from players */
	temp_min_distance_group_placement 20	/* Avoids other predators within 20 tiles */
}

if INFINITE_RESOURCES
else
	create_object TEMPOBJECT 
	{
		second_object STONE			            /* Uses TEMPOBJECT as placeholder for STONE */
		number_of_objects 3			            /* 3 STONE tiles per group */
		number_of_groups 2			            /* 2 groups per player */
		group_placement_radius 2		        /* 2-tile radius for group placement */
		min_distance_to_players 35		        /* Minimum 35 tiles from players */
		temp_min_distance_group_placement 24	/* Avoids other STONE groups within 24 tiles */
		set_gaia_object_only		            /* Belongs to Gaia */
		set_place_for_every_player		        /* Places STONE for each player */
		terrain_to_place_on DLC_ROADGRAVEL	    /* Places on road gravel terrain */
		set_tight_grouping			            /* STONE tiles spawn close together */
		find_closest				            /* Places at closest valid location */
		avoid_actor_area 82			            /* Avoids actor area 82 */
		avoid_forest_zone 2			            /* Avoids forests within 2 tiles */
		actor_area 92				            /* Actor area ID 92 */
		actor_area_radius 10			        /* 10-tile radius for actor area */
		avoid_actor_area 99			            /* Avoids actor area 5 */
		avoid_actor_area 9999			        /* Avoids actor area 9999 */
		avoid_actor_area 700			        /* Avoids actor area 700 */
		avoid_actor_area 5000			        /* Avoids actor area 5000 */
		avoid_actor_area 660			        /* Avoids actor area 660 */
	}
	create_object TEMPOBJECT 
	{
		second_object GOLD			            /* Uses TEMPOBJECT as placeholder for GOLD */
		number_of_objects 3			            /* 3 GOLD tiles per group */
		number_of_groups 2			            /* 2 groups per player */
		group_placement_radius 2		        /* 2-tile radius for group placement */
		min_distance_to_players 35		        /* Minimum 35 tiles from players */
		temp_min_distance_group_placement 24	/* Avoids other GOLD groups within 24 tiles */
		set_gaia_object_only		            /* Belongs to Gaia */
		set_place_for_every_player		        /* Places GOLD for each player */
		terrain_to_place_on DLC_ROADGRAVEL	    /* Places on road gravel terrain */
		set_tight_grouping			            /* GOLD tiles spawn close together */
		find_closest				            /* Places at closest valid location */
		avoid_actor_area 92			            /* Avoids actor area 92 */
		avoid_forest_zone 2			            /* Avoids forests within 2 tiles */
		actor_area 82				            /* Actor area ID 82 */
		actor_area_radius 10			        /* 10-tile radius for actor area */
		avoid_actor_area 99			            /* Avoids actor area 5 */
		avoid_actor_area 9999			        /* Avoids actor area 9999 */
		avoid_actor_area 5000			        /* Avoids actor area 5000 */
		avoid_actor_area 700			        /* Avoids actor area 700 */
		avoid_actor_area 660			        /* Avoids actor area 660 */
	}
endif